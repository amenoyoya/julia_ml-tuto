{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Julia言語で入門するプログラミング\n",
    "\n",
    "[Julia言語で入門するプログラミング](https://muuumin.net/julia-intro-prog/)\n",
    "\n",
    "## リファクタリング\n",
    "\n",
    "### 構造体\n",
    "現在、戦闘に関連するパラメーター（変数）は以下の6つを使っている\n",
    "\n",
    "```julia\n",
    "モンスターHP = 30\n",
    "モンスター攻撃力 = 10\n",
    "モンスター防御力 = 10\n",
    "勇者HP = 30\n",
    "勇者攻撃力 = 10\n",
    "勇者防御力 = 10\n",
    "```\n",
    "\n",
    "これは本来、モンスター／プレイヤーの2パターンと、それぞれが持つHP／攻撃力／防御力の3パターンである\n",
    "\n",
    "こういった場合 **構造体** を使って、複数の変数を1つにまとめるとすっきり記述できるようになる"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "勇者 = キャラクター(30, 20, 10)\n",
      "勇者HP: 30\n",
      "勇者攻撃力: 20\n",
      "勇者防御力: 10\n"
     ]
    }
   ],
   "source": [
    "struct キャラクター\n",
    "    HP::Int\n",
    "    攻撃力::Int\n",
    "    防御力::Int\n",
    "end\n",
    "\n",
    "# HP: 30, 攻撃力: 20, 防御力: 10 のキャラクターとして勇者を定義\n",
    "勇者 = キャラクター(30, 20, 10)\n",
    "\n",
    "@show 勇者\n",
    "println(\"勇者HP: $(勇者.HP)\")\n",
    "println(\"勇者攻撃力: $(勇者.攻撃力)\")\n",
    "println(\"勇者防御力: $(勇者.防御力)\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 可変構造体と module 宣言\n",
    "構造体はデフォルトで immutable である\n",
    "\n",
    "すなわち、構造体内部の変数を後から変更することはできない\n",
    "\n",
    "ゲームにおいては、HPなどの変数の値はどんどん変更されていくため、これでは困る\n",
    "\n",
    "こういった場合は `mutable struct` で可変構造体を宣言する\n",
    "\n",
    "この可変構造体を使って、前回の `main` 関数をリファクタリングしていくが、ここで一つ問題が発生する\n",
    "\n",
    "構造体のような型宣言は、Julia の REPL 上で同名の型を再定義できないのである\n",
    "\n",
    "そのため、今のまま `mutable struct キャラクター ... end` のように構造体を宣言しようとすると、`invalid redefinition of constant キャラクター` というようなエラーが発生する"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "ename": "LoadError",
     "evalue": "invalid redefinition of constant キャラクター",
     "output_type": "error",
     "traceback": [
      "invalid redefinition of constant キャラクター",
      "",
      "Stacktrace:",
      " [1] top-level scope",
      "   @ In[2]:1",
      " [2] eval",
      "   @ .\\boot.jl:373 [inlined]",
      " [3] include_string(mapexpr::typeof(REPL.softscope), mod::Module, code::String, filename::String)",
      "   @ Base .\\loading.jl:1196"
     ]
    }
   ],
   "source": [
    "mutable struct キャラクター\n",
    "    HP::Int\n",
    "    攻撃力::Int\n",
    "    防御力::Int\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "このような問題を回避するテクニックとして `module` を使う方法がある\n",
    "\n",
    "`module` は本来、特定の機能に関連する型宣言・関数・変数等を一つのパッケージにまとめるための機能だが、`module` 自体は REPL 上で再定義可能なため、型宣言などを再定義するのに使うことができる\n",
    "\n",
    "今回は、このゲームプログラムを `RPG` module として定義することにして、前回の `main` 関数をリファクタリングしていく"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "モンスターに遭遇した！\n",
      "戦闘開始！\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：20\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：20\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：10\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：10\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：0\n",
      "戦闘に敗北した・・・\n",
      "モンスターに遭遇した！\n",
      "戦闘開始！\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：20\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：20\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：10\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：10\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：0\n",
      "戦闘に勝利した！\n",
      "\u001b[0m\u001b[1mTest Summary: | \u001b[22m\u001b[32m\u001b[1mPass  \u001b[22m\u001b[39m\u001b[36m\u001b[1mTotal\u001b[22m\u001b[39m\n",
      "main関数      | \u001b[32m   2  \u001b[39m\u001b[36m    2\u001b[39m\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "Main.RPG"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# このゲームプログラムを RPG module としてまとめる\n",
    "module RPG\n",
    "\n",
    "using Test\n",
    "\n",
    "\"HP計算関数\"\n",
    "function HP計算(HP::Int, ダメージ量::Int)\n",
    "    ダメージ後のHP = HP - ダメージ量\n",
    "    if ダメージ後のHP < 0\n",
    "        return 0\n",
    "    end\n",
    "    return ダメージ後のHP\n",
    "end\n",
    "\n",
    "\"ダメージ計算関数\"\n",
    "function ダメージ計算(攻撃力::Int, 防御力::Int)\n",
    "    if 攻撃力 < 0 || 防御力 < 0\n",
    "        throw(DomainError((攻撃力, 防御力), \"攻撃力, 防御力は正の値である必要があります\")) \n",
    "    end\n",
    "    if 防御力 === 0\n",
    "        throw(DomainError(防御力, \"防御力は0より大きい値である必要があります\")) \n",
    "    end\n",
    "    ダメージ量 = round(Int, 10 * 攻撃力 / 防御力)\n",
    "end\n",
    "\n",
    "\"キャラクター可変構造体\"\n",
    "mutable struct キャラクター\n",
    "    HP::Int\n",
    "    攻撃力::Int\n",
    "    防御力::Int\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    main(乱数生成関数::Function) ->  戦闘ターン数::Int,\n",
    "                                    戦闘後の勇者HP::Int,\n",
    "                                    戦闘後のモンスターHP::Int\n",
    "\n",
    "自動テスト可能な main 関数\n",
    "\"\"\"\n",
    "function main(乱数生成関数::Function)\n",
    "    # 各キャラクターのパラメータを構造体にまとめる\n",
    "    モンスター = キャラクター(30, 10, 10)\n",
    "    勇者 = キャラクター(30, 10, 10)\n",
    "\n",
    "    戦闘ターン数 = 1\n",
    "\n",
    "    println(\"モンスターに遭遇した！\")\n",
    "    println(\"戦闘開始！\")\n",
    "\n",
    "    while 勇者.HP > 0 && モンスター.HP > 0\n",
    "        if 乱数生成関数() < 0.5\n",
    "            # 50％の確率で勇者が先制攻撃\n",
    "            println(\"----------\")\n",
    "            println(\"勇者の攻撃！\")\n",
    "            モンスターダメージ = ダメージ計算(勇者.攻撃力, モンスター.防御力)\n",
    "            モンスター.HP = HP計算(モンスター.HP, モンスターダメージ)\n",
    "            println(\"モンスターは $(モンスターダメージ) のダメージを受けた！\")\n",
    "            println(\"モンスターの残りHP：$(モンスター.HP)\")\n",
    "\n",
    "            if モンスター.HP === 0\n",
    "                break\n",
    "            end\n",
    "\n",
    "            println(\"----------\")\n",
    "            println(\"モンスターの攻撃！\")\n",
    "            勇者ダメージ = ダメージ計算(モンスター.攻撃力, 勇者.防御力)\n",
    "            勇者.HP = HP計算(勇者.HP, 勇者ダメージ)\n",
    "            println(\"勇者は $(勇者ダメージ) のダメージを受けた！\")\n",
    "            println(\"勇者の残りHP：$(勇者.HP)\")\n",
    "\n",
    "            if 勇者.HP === 0\n",
    "                break\n",
    "            end\n",
    "        else\n",
    "            # 50％の確率でモンスターが先制攻撃\n",
    "            println(\"----------\")\n",
    "            println(\"モンスターの攻撃！\")\n",
    "            勇者ダメージ = ダメージ計算(モンスター.攻撃力, 勇者.防御力)\n",
    "            勇者.HP = HP計算(勇者.HP, 勇者ダメージ)\n",
    "            println(\"勇者は $(勇者ダメージ) のダメージを受けた！\")\n",
    "            println(\"勇者の残りHP：$(勇者.HP)\")\n",
    "\n",
    "            if 勇者.HP === 0\n",
    "                break\n",
    "            end\n",
    "\n",
    "            println(\"----------\")\n",
    "            println(\"勇者の攻撃！\")\n",
    "            モンスターダメージ = ダメージ計算(勇者.攻撃力, モンスター.防御力)\n",
    "            モンスター.HP = HP計算(モンスター.HP, モンスターダメージ)\n",
    "            println(\"モンスターは $(モンスターダメージ) のダメージを受けた！\")\n",
    "            println(\"モンスターの残りHP：$(モンスター.HP)\")\n",
    "\n",
    "            if モンスター.HP === 0\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "\n",
    "        # 戦闘ターン数 を 1 増やす\n",
    "        戦闘ターン数 += 1\n",
    "    end\n",
    "\n",
    "    # モンスターHPが 0 になっているかどうかで戦闘終了メッセージを変える\n",
    "    if モンスター.HP === 0\n",
    "        println(\"戦闘に勝利した！\")\n",
    "    else\n",
    "        println(\"戦闘に敗北した・・・\")\n",
    "    end\n",
    "\n",
    "    # テストしたい値を返す\n",
    "    戦闘ターン数, 勇者.HP, モンスター.HP\n",
    "end\n",
    "\n",
    "#=\n",
    "自動テスト: 乱数生成関数が出力する数列をもとに、戦闘ターン数と戦闘後の勇者・モンスターのHPをテストする\n",
    "\n",
    "- [0.5, 0.4, 0.6]:\n",
    "    1. モンスター(HP: 30) -> 勇者(HP: 30) 攻撃 ==> 勇者(HP: 20) -> モンスター(HP: 30) 攻撃\n",
    "    2. 勇者(HP: 20) -> モンスター(HP: 20) 攻撃 ==> モンスター(HP: 10) -> 勇者(HP: 20) 攻撃\n",
    "    3. モンスター(HP: 10) -> 勇者(HP: 10) 攻撃 ==> 戦闘終了: 勇者(HP: 0), モンスター(HP: 10)\n",
    "- [0.4, 0.7, 0.3]:\n",
    "    1. 勇者(HP: 30) -> モンスター(HP: 30) 攻撃 ==> モンスター(HP: 20) -> 勇者(HP: 30) 攻撃\n",
    "    2. モンスター(HP: 20) -> 勇者(HP: 20) 攻撃 ==> 勇者(HP: 10) -> モンスター(HP: 20) 攻撃\n",
    "    3. 勇者(HP: 10) -> モンスター(HP: 10) ==> 戦闘終了: 勇者(HP: 10), モンスター(HP: 0)\n",
    "=#\n",
    "function 疑似乱数生成高階関数(疑似乱数配列::Vector{<:Number})\n",
    "    function 疑似乱数生成関数()\n",
    "        popfirst!(疑似乱数配列)\n",
    "    end\n",
    "end\n",
    "\n",
    "@testset \"main関数\" begin\n",
    "    @test main(疑似乱数生成高階関数([0.5, 0.4, 0.6])) === (3, 0, 10)\n",
    "    @test main(疑似乱数生成高階関数([0.4, 0.7, 0.3])) === (3, 10, 0)\n",
    "end\n",
    "\n",
    "end # module RPG"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "コードが多少すっきりしてきたが、まだ冗長な感じがする\n",
    "\n",
    "これは、攻撃を行っている部分のコードがほぼコピペで出来ているためと思われる\n",
    "\n",
    "実際のところ、攻撃しているのが勇者かモンスターかの違いだけで、その中の処理は全く同じである\n",
    "\n",
    "そのため、この攻撃部分の処理を以下のような関数にまとめると、かなりすっきり書けそうである\n",
    "\n",
    "```julia\n",
    "function 攻撃処理!(攻撃者::キャラクター, 防御者::キャラクター)\n",
    "    ...\n",
    "end\n",
    "```\n",
    "\n",
    "Julia の関数名の慣例として、引数に渡した構造体の値を変更するような関数は `!` (\"bang\") をつけると良い\n",
    "\n",
    "この \"bang\" マークがあることにより、関数の実行前後で、構造体内部変数に変更が生じていることが明確になる\n",
    "\n",
    "今回の場合は、`攻撃処理!` 関数実行後、`防御者.HP` が変更されている（減っている）はずである"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "モンスターに遭遇した！\n",
      "戦闘開始！\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：20\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：20\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：10\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：10\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：0\n",
      "戦闘に敗北した・・・\n",
      "モンスターに遭遇した！\n",
      "戦闘開始！\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：20\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：20\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：10\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：10\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：0\n",
      "戦闘に勝利した！\n",
      "\u001b[0m\u001b[1mTest Summary: | \u001b[22m\u001b[32m\u001b[1mPass  \u001b[22m\u001b[39m\u001b[36m\u001b[1mTotal\u001b[22m\u001b[39m\n",
      "main関数      | \u001b[32m   2  \u001b[39m\u001b[36m    2\u001b[39m\n"
     ]
    },
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "WARNING: replacing module RPG.\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "Main.RPG"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# HP計算, ダメージ計算 関数は使いまわしたいため退避しておく\n",
    "import .RPG\n",
    "HP計算 = RPG.HP計算\n",
    "ダメージ計算 = RPG.ダメージ計算\n",
    "\n",
    "# --------------------------------------------------\n",
    "# RPG module の再定義\n",
    "module RPG\n",
    "\n",
    "using Test\n",
    "\n",
    "# 親 module (RPG module の外側) に退避していた HP計算, ダメージ計算 関数を import\n",
    "import ..HP計算, ..ダメージ計算\n",
    "\n",
    "\"キャラクター可変構造体\"\n",
    "mutable struct キャラクター\n",
    "    名前::String # 追加\n",
    "    HP::Int\n",
    "    攻撃力::Int\n",
    "    防御力::Int\n",
    "end\n",
    "\n",
    "\"攻撃処理関数\"\n",
    "function 攻撃処理!(攻撃者::キャラクター, 防御者::キャラクター)\n",
    "    println(\"----------\")\n",
    "    println(\"$(攻撃者.名前)の攻撃！\")\n",
    "    防御者ダメージ = ダメージ計算(攻撃者.攻撃力, 防御者.防御力)\n",
    "    防御者.HP = HP計算(防御者.HP, 防御者ダメージ)\n",
    "    println(\"$(防御者.名前)は $(防御者ダメージ) のダメージを受けた！\")\n",
    "    println(\"$(防御者.名前)の残りHP：$(防御者.HP)\")\n",
    "end\n",
    "\n",
    "\"main関数\"\n",
    "function main(乱数生成関数::Function)\n",
    "    モンスター = キャラクター(\"モンスター\", 30, 10, 10)\n",
    "    勇者 = キャラクター(\"勇者\", 30, 10, 10)\n",
    "\n",
    "    戦闘ターン数 = 1\n",
    "\n",
    "    println(\"モンスターに遭遇した！\")\n",
    "    println(\"戦闘開始！\")\n",
    "\n",
    "    while 勇者.HP > 0 && モンスター.HP > 0\n",
    "        if 乱数生成関数() < 0.5\n",
    "            # 50％の確率で勇者が先制攻撃\n",
    "            攻撃処理!(勇者, モンスター)\n",
    "            if モンスター.HP === 0\n",
    "                break\n",
    "            end\n",
    "\n",
    "            攻撃処理!(モンスター, 勇者)\n",
    "            if 勇者.HP === 0\n",
    "                break\n",
    "            end\n",
    "        else\n",
    "            # 50％の確率でモンスターが先制攻撃\n",
    "            攻撃処理!(モンスター, 勇者)\n",
    "            if 勇者.HP === 0\n",
    "                break\n",
    "            end\n",
    "\n",
    "            攻撃処理!(勇者, モンスター)\n",
    "            if モンスター.HP === 0\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "\n",
    "        戦闘ターン数 += 1\n",
    "    end\n",
    "\n",
    "    if モンスター.HP === 0\n",
    "        println(\"戦闘に勝利した！\")\n",
    "    else\n",
    "        println(\"戦闘に敗北した・・・\")\n",
    "    end\n",
    "\n",
    "    戦闘ターン数, 勇者.HP, モンスター.HP\n",
    "end\n",
    "\n",
    "# test\n",
    "function 疑似乱数生成高階関数(疑似乱数配列::Vector{<:Number})\n",
    "    function 疑似乱数生成関数()\n",
    "        popfirst!(疑似乱数配列)\n",
    "    end\n",
    "end\n",
    "\n",
    "@testset \"main関数\" begin\n",
    "    @test main(疑似乱数生成高階関数([0.5, 0.4, 0.6])) === (3, 0, 10)\n",
    "    @test main(疑似乱数生成高階関数([0.4, 0.7, 0.3])) === (3, 10, 0)\n",
    "end\n",
    "\n",
    "end # module RPG"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "だいぶすっきりしたコードになってきた\n",
    "\n",
    "とはいえ、`while` ループの中がまだ少し長い\n",
    "\n",
    "ここではもう少し踏み込んで、「行動順の決定」と「ゲームループ」部分を別関数に分けてみる"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "モンスターに遭遇した！\n",
      "戦闘開始！\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：20\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：20\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：10\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：10\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：0\n",
      "戦闘に敗北した・・・\n",
      "モンスターに遭遇した！\n",
      "戦闘開始！\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：20\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：20\n",
      "----------\n",
      "モンスターの攻撃！\n",
      "勇者は 10 のダメージを受けた！\n",
      "勇者の残りHP：10\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：10\n",
      "----------\n",
      "勇者の攻撃！\n",
      "モンスターは 10 のダメージを受けた！\n",
      "モンスターの残りHP：0\n",
      "戦闘に勝利した！\n",
      "\u001b[0m\u001b[1mTest Summary: | \u001b[22m\u001b[32m\u001b[1mPass  \u001b[22m\u001b[39m\u001b[36m\u001b[1mTotal\u001b[22m\u001b[39m\n",
      "main関数      | \u001b[32m   2  \u001b[39m\u001b[36m    2\u001b[39m\n"
     ]
    },
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "WARNING: replacing module RPG.\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "Main.RPG"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "import .RPG\n",
    "HP計算 = RPG.HP計算\n",
    "ダメージ計算 = RPG.ダメージ計算\n",
    "攻撃処理! = RPG.攻撃処理!\n",
    "疑似乱数生成高階関数 = RPG.疑似乱数生成高階関数\n",
    "\n",
    "# キャラクター構造体を一時退避\n",
    "## Main.キャラクター は struct 型で定義済のため別の変数名で退避する必要がある\n",
    "キャラクター構造体 = RPG.キャラクター\n",
    "\n",
    "# --------------------------------------------------\n",
    "module RPG\n",
    "\n",
    "using Test\n",
    "\n",
    "import ..HP計算, ..ダメージ計算, ..攻撃処理!, ..疑似乱数生成高階関数, ..キャラクター構造体\n",
    "\n",
    "キャラクター = キャラクター構造体\n",
    "\n",
    "\"\"\"\n",
    "    行動順決定(勇者::キャラクター, モンスター::キャラクター, 乱数::Number)\n",
    "        -> [(攻撃者::キャラクター, 防御者::キャラクター), ...]\n",
    "\n",
    "行動順決定関数\n",
    "\"\"\"\n",
    "function 行動順決定(勇者::キャラクター, モンスター::キャラクター, 乱数::Number)\n",
    "    if 乱数 < 0.5\n",
    "        return [(勇者, モンスター), (モンスター, 勇者)]\n",
    "    else\n",
    "        return [(モンスター, 勇者), (勇者, モンスター)]\n",
    "    end\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    ゲームループ!(勇者::キャラクター, モンスター::キャラクター, 乱数生成関数::Function)\n",
    "        -> 戦闘ターン数::Int\n",
    "\n",
    "ゲームループ処理関数\n",
    "\"\"\"\n",
    "function ゲームループ!(勇者::キャラクター, モンスター::キャラクター, 乱数生成関数::Function)\n",
    "    戦闘ターン数 = 1\n",
    "\n",
    "    while 勇者.HP > 0 && モンスター.HP > 0\n",
    "        for (攻撃者, 防御者) in 行動順決定(勇者, モンスター, 乱数生成関数())\n",
    "            攻撃処理!(攻撃者, 防御者)\n",
    "            if 防御者.HP === 0\n",
    "                return 戦闘ターン数 # ゲームループ終了\n",
    "            end\n",
    "        end\n",
    "        戦闘ターン数 += 1\n",
    "    end\n",
    "\n",
    "    # 戦闘ターン数を返す\n",
    "    戦闘ターン数\n",
    "end\n",
    "\n",
    "\"main関数\"\n",
    "function main(乱数生成関数::Function)\n",
    "    モンスター = キャラクター(\"モンスター\", 30, 10, 10)\n",
    "    勇者 = キャラクター(\"勇者\", 30, 10, 10)\n",
    "\n",
    "    println(\"モンスターに遭遇した！\")\n",
    "    println(\"戦闘開始！\")\n",
    "\n",
    "    戦闘ターン数 = ゲームループ!(勇者, モンスター, 乱数生成関数)\n",
    "\n",
    "    if モンスター.HP === 0\n",
    "        println(\"戦闘に勝利した！\")\n",
    "    else\n",
    "        println(\"戦闘に敗北した・・・\")\n",
    "    end\n",
    "\n",
    "    戦闘ターン数, 勇者.HP, モンスター.HP\n",
    "end\n",
    "\n",
    "# test\n",
    "@testset \"main関数\" begin\n",
    "    @test main(疑似乱数生成高階関数([0.5, 0.4, 0.6])) === (3, 0, 10)\n",
    "    @test main(疑似乱数生成高階関数([0.4, 0.7, 0.3])) === (3, 10, 0)\n",
    "end\n",
    "\n",
    "end # module RPG"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "これで `main` 関数はだいぶすっきりしたのではないだろうか\n",
    "\n",
    "このように、一つまとまりになっているデータや処理は、構造体や関数に上手く分離していくときれいなコードを書けるようになる\n",
    "\n",
    "ゲームとしての完成度はともかく、これで一通りのリファクタリングは完了である"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7 with sysimage 1.7.2",
   "language": "julia",
   "name": "julia-1.7-with-sysimage-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.2"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
